plugins {
    id 'java'
}

repositories {
    mavenLocal()
    maven {
        url = 'https://repo.runelite.net'
        content {
            includeGroupByRegex("net\\.runelite.*")
        }
    }
    mavenCentral()
}

def runeLiteVersion = 'latest.release'

dependencies {
    compileOnly group: 'net.runelite', name: 'client', version: runeLiteVersion
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    
    implementation 'com.google.code.gson:gson:2.10.1'
    
    testImplementation group: 'net.runelite', name: 'client', version: runeLiteVersion
    testImplementation group: 'net.runelite', name: 'jshell', version: runeLiteVersion
}

group = 'com.osrsrl'
version = '1.0.0'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(11)
}

tasks.register('run', JavaExec) {
    group = 'application'
    description = 'Run RuneLite with OSRS-RL plugin'
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'com.osrsrl.OSRSRLPluginTest'
    jvmArgs '-ea', '-Xmx2g'
    args '--developer-mode', '--debug'
}

// Create fat jar for distribution
tasks.register('fatJar', Jar) {
    archiveClassifier = 'all'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
